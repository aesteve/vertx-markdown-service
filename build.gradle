plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
}

def vertxVersion = '3.1.0'

repositories {
    mavenCentral()
}

dependencies {
    // vertx
    compile "io.vertx:vertx-core:$vertxVersion"
	compile "io.vertx:vertx-service-proxy:$vertxVersion"
	compile "io.vertx:vertx-codegen:$vertxVersion"
	
	// Pegdown implementation
	compile 'org.pegdown:pegdown:1.5.0'
    
    // tests
    testCompile group: 'io.vertx', name: 'vertx-unit', version: vertxVersion
    testCompile 'junit:junit:4.12'
}

def generatedSrcPath="$buildDir/generated-src"
def generatedSrcDir = file(generatedSrcPath)

sourceSets {
	main {
		java.srcDirs += generatedSrcDir
		output.dir(builtBy: 'generateServiceProxy', generatedSrcPath)
	}
}

task generateServiceProxy(type: JavaCompile, description: 'Generates EBServiceProxies') {
	source = sourceSets.main.java
	classpath = configurations.compile
	destinationDir = generatedSrcDir
	options.compilerArgs = [
		"-proc:only",
		"-processor", "io.vertx.codegen.CodeGenProcessor",
		"-AoutputDirectory=$generatedSrcPath"
	]
}

compileJava.dependsOn generateServiceProxy

clean {
	delete += generatedSrcDir
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.8'
}
